dipp

medium

# Can remove liquidity without providing vault tokens in ```ArrakisV2Router.sol```

## Summary

Lack of checks in the ```removeLiquidity``` function in ```ArrakisV2Router.sol``` could allow malcious users to steal underlying tokens from any vault when the router contract contains vault tokens.

## Vulnerability Detail

The ```removeLiquidity``` function in ```ArrakisV2Router.sol``` takes a user's vault tokens to burn them and remove the underlying tokens from the vault. If no gauge is given in params_ then the vault tokens are sent directly from the user to the router, otherwise the router contract attempts to retrieve the gauge tokens from the user and use the gauge tokens to withdraw vault tokens from the gauge.

```solidity
    function removeLiquidity(RemoveLiquidityData memory params_)
        external
        whenNotPaused
        nonReentrant
        returns (uint256 amount0, uint256 amount1)
    {
        require(params_.burnAmount > 0, "nothing to burn");
        if (params_.gauge != address(0)) {
            require(
                params_.vault == IGauge(params_.gauge).staking_token(),
                "Incorrect gauge!"
            );
            IGauge(params_.gauge).claim_rewards(msg.sender);
            IERC20(params_.gauge).safeTransferFrom(
                msg.sender,
                address(this),
                params_.burnAmount
            );

            IGauge(params_.gauge).withdraw(params_.burnAmount);
        } else {
            IERC20(params_.vault).safeTransferFrom(
                msg.sender,
                address(this),
                params_.burnAmount
            );
        }

        (amount0, amount1) = _removeLiquidity(params_);
    }
```

If the gauge is a malicious contract that does not send vault tokens after ```withdraw``` is called then the router does not receive any vault tokens from the user. It might also be possible that the gauge sends tokens but less than the expected ```burnAmount``` tokens.

Regardless of whether it received vault tokens or not, the ```_removeLiquidity``` function is called which will burn the ```removeData.burnAmount``` vault tokens. The underlying amounts are then sent directly to the receiver specifed by the caller of ```removeLiquidity```.

```solidity
    function _removeLiquidity(RemoveLiquidityData memory removeData_)
        internal
        returns (uint256 amount0, uint256 amount1)
    {
        if (removeData_.receiveETH) {
            (amount0, amount1) = IArrakisV2(removeData_.vault).burn(
                removeData_.burnAmount,
                address(this)
            );
        } else {
            (amount0, amount1) = IArrakisV2(removeData_.vault).burn(
                removeData_.burnAmount,
                removeData_.receiver
            );
        }

        require(
            amount0 >= removeData_.amount0Min &&
                amount1 >= removeData_.amount1Min,
            "received below minimum"
        );

        if (removeData_.receiveETH) {
            _receiveETH(
                IArrakisV2(removeData_.vault),
                amount0,
                amount1,
                removeData_.receiver
            );
        }
    }
```

## Impact

Users may be able to steal underlying tokens from any vault through the router. 

This would only be possible when the router contract has vault tokens, which should not happen intentionally. Therefore, the impact is limited to vault tokens sent on accident to the router or through a bug.

## Code Snippet

[ArrakisV2Router.sol#L201-L230](https://github.com/sherlock-audit/2023-06-arrakis/blob/main/v2-periphery/contracts/ArrakisV2Router.sol#L201-L230)

[ArrakisV2Router.sol#L536-L566](https://github.com/sherlock-audit/2023-06-arrakis/blob/main/v2-periphery/contracts/ArrakisV2Router.sol#L536-L566)

## Tool used

Manual Review

## Recommendation

Consider checking the change in vault token balance before and after withdrawing from the gauge to ensure a minimum of burnAmount tokens were sent.