Madalad

medium

# `ArrakisV2#mint`: `denominator` variable used for initial mint should depend on token decimals

## Summary

When calling `mint`, there is separate logic if the vaults current `totalSupply` is 0. Here, a `denominator` is used in calculation and is always equal to `1 ether`, which can cause issues for certain tokens and certain `init0M`, `init1M` values.

## Vulnerability Detail

Let's assume that for this vault token0 is USDC, token1 is WETH, `init0M = 1` and `init1M = 0` (these are the parameters for the vault in the [test suite](https://github.com/ArrakisFinance/v2-core/blob/9133fc412b65c7a902f62f1ad135f062e927b092/test/unit_tests/ArrakisV2.test.ts#L40-L49)). The first minter calls `mint` with `mintAmount_ = 1 ether`.

The function will enter the [else block](https://github.com/ArrakisFinance/v2-core/blob/9133fc412b65c7a902f62f1ad135f062e927b092/contracts/ArrakisV2.sol#L79-L115) and first compute amount0 and amount1, rounding up:
```solidity
            amount0 = FullMath.mulDivRoundingUp(
                mintAmount_,
                init0M,
                denominator
            ); // @audit amount0 = 1 ether * 1 / 1 ether = 1
            amount1 = FullMath.mulDivRoundingUp(
                mintAmount_,
                init1M,
                denominator
            ); // @audit amount1 = 1 ether * 0 / 1 ether = 0
```

We have that `amount0` = 1 and `amount1` = 0. Then, `amount0Mint` and `amount1Mint` are calculated:
```solidity
            uint256 amount0Mint = init0M != 0
                ? FullMath.mulDiv(amount0, denominator, init0M)
                : type(uint256).max;
            // @audit amount0Mint = 1 * 1 ether / 1 = 1 ether
            uint256 amount1Mint = init1M != 0
                ? FullMath.mulDiv(amount1, denominator, init1M)
                : type(uint256).max;
            // @audit amount1Mint = type(uint256).max
```

Now `amount0Mint` = 1 ether and `amount1Mint` = `type(uint256).max`, and the [require](https://github.com/ArrakisFinance/v2-core/blob/9133fc412b65c7a902f62f1ad135f062e927b092/contracts/ArrakisV2.sol#L110-L114) check passes:
```solidity
            require(
                (amount0Mint < amount1Mint ? amount0Mint : amount1Mint) ==
                    mintAmount_,
                "A0&A1"
            );
```

Then, the vault shares are minted to the `receiver_` and tokens are transferred from the sender to the vault:
```solidity
        _mint(receiver_, mintAmount_);

        // transfer amounts owed to contract
        if (amount0 > 0) {
            token0.safeTransferFrom(msg.sender, me, amount0);
        }
        if (amount1 > 0) {
            token1.safeTransferFrom(msg.sender, me, amount1);
        }
```

Since USDC has 6 decimals, and `amount0` = 1 ether, the vault contract will attempt to transfer 1e12 USDC, which is $1,000,000,000,000 ($1 trillion), which exceeds the total supply of USDC (which is 26763141093835371 ~= 0.02 ether at the time of writing).

Note that setting a `mintAmount_` lower than 1 ether would not solve the problem, as this would lead to a rounding issue that is correctly detected by the function logic, causing a revert when the require check does not pass.

## Impact

If a vault is setup as it is in the test suite, or in any similar fashion, the initial mint would always revert, essentially rendering the vault impossible to interact with indefinitely. In fact, it is impossible to set up a vault for which the initial deposit receives USDC, or any other token with sufficiently few decimals.

## Code Snippet

```solidity
        if (isTotalSupplyGtZero) {
            (amount0, amount1) = UnderlyingHelper.totalUnderlyingForMint(
                UnderlyingPayload({
                    ranges: _ranges,
                    factory: factory,
                    token0: address(token0),
                    token1: address(token1),
                    self: me
                }),
                mintAmount_,
                ts
            );
        } else {
            uint256 denominator = 1 ether;
            uint256 init0M = init0;
            uint256 init1M = init1;

            amount0 = FullMath.mulDivRoundingUp(
                mintAmount_,
                init0M,
                denominator
            );
            amount1 = FullMath.mulDivRoundingUp(
                mintAmount_,
                init1M,
                denominator
            );

            /// @dev check ratio against small values that skew init ratio
            if (FullMath.mulDiv(mintAmount_, init0M, denominator) == 0) {
                amount0 = 0;
            }
            if (FullMath.mulDiv(mintAmount_, init1M, denominator) == 0) {
                amount1 = 0;
            }

            uint256 amount0Mint = init0M != 0
                ? FullMath.mulDiv(amount0, denominator, init0M)
                : type(uint256).max;
            uint256 amount1Mint = init1M != 0
                ? FullMath.mulDiv(amount1, denominator, init1M)
                : type(uint256).max;
            
            require(
                (amount0Mint < amount1Mint ? amount0Mint : amount1Mint) ==
                    mintAmount_,
                "A0&A1"
            );
        }

        _mint(receiver_, mintAmount_);

        // transfer amounts owed to contract
        if (amount0 > 0) {
            token0.safeTransferFrom(msg.sender, me, amount0);
        }
        if (amount1 > 0) {
            token1.safeTransferFrom(msg.sender, me, amount1);
        }
```
https://github.com/ArrakisFinance/v2-core/blob/9133fc412b65c7a902f62f1ad135f062e927b092/contracts/ArrakisV2.sol#L67-L115

## Tool used

Manual Review

## Recommendation

Make `denominator` depend on the number of decimals of each token.

```diff
        } else {
-           uint256 denominator = 1 ether;
+           uint256 denominator0 = token0.decimals();
+           uint256 denominator1 = token1.decimals();
            uint256 init0M = init0;
            uint256 init1M = init1;
```