0xbepresent

medium

# Adding a whitelist address via `ArrakisV2RouterStorage.whitelist()` function is not independent from the supply cap configuration

## Summary

It is necessary to configure a [positive supply cap](https://github.com/sherlock-audit/2023-06-arrakis/blob/main/v2-periphery/contracts/abstract/ArrakisV2RouterStorage.sol#L102) if the owner wants to use the [hasWhitelist](https://github.com/sherlock-audit/2023-06-arrakis/blob/main/v2-periphery/contracts/abstract/ArrakisV2RouterStorage.sol#L103C13-L103C25) configuration. So the owner can't configure a mint whitelist with a `supply cap zero`, that could be incorrect because the owner may have a vault without supply cap limitation but with a white minters list.

## Vulnerability Detail

The ArrakisV2Router owner can set a [supply cap](https://github.com/sherlock-audit/2023-06-arrakis/blob/main/v2-periphery/contracts/abstract/ArrakisV2RouterStorage.sol#L102) and [hasWhitelist](https://github.com/sherlock-audit/2023-06-arrakis/blob/main/v2-periphery/contracts/abstract/ArrakisV2RouterStorage.sol#L103C13-L103C25) configurations via [ArrakisV2RouterStorage.setMintRules()](https://github.com/sherlock-audit/2023-06-arrakis/blob/main/v2-periphery/contracts/abstract/ArrakisV2RouterStorage.sol#L93C14-L93C26) function. Then, when someone add liquidity via [ArrakisV2Router.addLiquidity()](https://github.com/sherlock-audit/2023-06-arrakis/blob/main/v2-periphery/contracts/ArrakisV2Router.sol#L50) the configurations are validated [here](https://github.com/sherlock-audit/2023-06-arrakis/blob/main/v2-periphery/contracts/ArrakisV2Router.sol#L413-L427):

If the supply cap configuration is positive, then it validates that the mint amount is not higher than the limits:

```solidity
File: ArrakisV2Router.sol
399:     function _addLiquidity(
400:         address vault_,
401:         uint256 amount0In_,
402:         uint256 amount1In_,
403:         uint256 mintAmount_,
404:         address gauge_,
405:         address receiver_,
406:         IERC20 token0_,
407:         IERC20 token1_
408:     ) internal {
...
...
412:         {
413:             MintRules memory mintRules = mintRestrictedVaults[vault_];
414:             if (mintRules.supplyCap > 0) {
415:                 require(
416:                     IArrakisV2(vault_).totalSupply() + mintAmount_ <=
417:                         mintRules.supplyCap,
418:                     "above supply cap"
419:                 );
420:             }
...
...
427:         }
```

Additionally, if the `hasWhitelist` configuration is `True`, then it validates if the `msg.sender` is in the `_mintWhitelist` list:

```solidity
File: ArrakisV2Router.sol
399:     function _addLiquidity(
400:         address vault_,
401:         uint256 amount0In_,
402:         uint256 amount1In_,
403:         uint256 mintAmount_,
404:         address gauge_,
405:         address receiver_,
406:         IERC20 token0_,
407:         IERC20 token1_
408:     ) internal {
...
...
412:         {
413:             MintRules memory mintRules = mintRestrictedVaults[vault_];
...
...
421:             if (mintRules.hasWhitelist) {
422:                 require(
423:                     _mintWhitelist[vault_].contains(msg.sender),
424:                     "not whitelisted"
425:                 );
426:             }
427:         }
```

So the `supplyCap` and `hasWhitelist` are evaluated independently of each other.

The problem here is when the owner wants to add a whitelist minter but the [whitelist()](https://github.com/sherlock-audit/2023-06-arrakis/blob/main/v2-periphery/contracts/abstract/ArrakisV2RouterStorage.sol#L109) function checks if the supply cap is not zero in the [code line 113](https://github.com/sherlock-audit/2023-06-arrakis/blob/main/v2-periphery/contracts/abstract/ArrakisV2RouterStorage.sol#L113):

```solidity
File: ArrakisV2RouterStorage.sol
109:     function whitelist(address vault_, address[] memory toWhitelist_)
110:         external
111:         onlyOwner
112:     {
113:         require(mintRestrictedVaults[vault_].supplyCap > 0, "vault not set");
114:         for (uint256 i; i < toWhitelist_.length; i++) {
115:             require(_mintWhitelist[vault_].add(toWhitelist_[i]), "add failed");
116:         }
117: 
118:         emit LogWhitelist(vault_, toWhitelist_);
119:     }
```

That could be a problem when an owner wants to have whitelisted minters addresses without any supply cap (supplyCap = 0). So for example:
1. The owner add `supplyCap=0` and `hasWhitelist=True` via [setMintRules()](https://github.com/sherlock-audit/2023-06-arrakis/blob/main/v2-periphery/contracts/abstract/ArrakisV2RouterStorage.sol#L93C14-L93C26)
2. Then, the addLiquidity action is not possible if [the msg.sender is not in the _mintWhitelist](https://github.com/sherlock-audit/2023-06-arrakis/blob/main/v2-periphery/contracts/ArrakisV2Router.sol#L422)
3. The owner wants to add a minter address to the `_mintWhitelist` but is not possible because the [supplyCap is zero](https://github.com/sherlock-audit/2023-06-arrakis/blob/main/v2-periphery/contracts/abstract/ArrakisV2RouterStorage.sol#L113).
4. The owner is forced to set a positive `supplyCap` if he wants to use whitelisted minters.

## Impact

The owner can't set whitelisted minters via [whitelist()](https://github.com/sherlock-audit/2023-06-arrakis/blob/main/v2-periphery/contracts/abstract/ArrakisV2RouterStorage.sol#L113) unless he set a positive `supplyCap`. That could be a problem because there could be situations where whitelisted minters are necessary but a `positive supply cap` is not necessary.


## Code Snippet

The [ArrakisV2RouterStorage.setMintRules()](https://github.com/sherlock-audit/2023-06-arrakis/blob/main/v2-periphery/contracts/abstract/ArrakisV2RouterStorage.sol#L93C14-L93C26):

```solidity
File: ArrakisV2RouterStorage.sol
093:     function setMintRules(
094:         address vault_,
095:         uint256 supplyCap_,
096:         bool hasWhitelist_
097:     ) external onlyOwner {
098:         require(supplyCap_ > 0, "zero");
099:         address minter = IArrakisV2(vault_).restrictedMint();
100:         require(minter == address(this), "must be minter");
101:         mintRestrictedVaults[vault_] = MintRules({
102:             supplyCap: supplyCap_,
103:             hasWhitelist: hasWhitelist_
104:         });
105: 
106:         emit LogSetVault(vault_, supplyCap_, hasWhitelist_);
107:     }
```

The [whitelist()](https://github.com/sherlock-audit/2023-06-arrakis/blob/main/v2-periphery/contracts/abstract/ArrakisV2RouterStorage.sol#L109) function:

```solidity
File: ArrakisV2RouterStorage.sol
109:     function whitelist(address vault_, address[] memory toWhitelist_)
110:         external
111:         onlyOwner
112:     {
113:         require(mintRestrictedVaults[vault_].supplyCap > 0, "vault not set");
114:         for (uint256 i; i < toWhitelist_.length; i++) {
115:             require(_mintWhitelist[vault_].add(toWhitelist_[i]), "add failed");
116:         }
117: 
118:         emit LogWhitelist(vault_, toWhitelist_);
119:     }
```

The [addLiquidity](https://github.com/sherlock-audit/2023-06-arrakis/blob/main/v2-periphery/contracts/ArrakisV2Router.sol#L413-L427) validations:

```solidity
    function _addLiquidity(
        address vault_,
        uint256 amount0In_,
        uint256 amount1In_,
        uint256 mintAmount_,
        address gauge_,
        address receiver_,
        IERC20 token0_,
        IERC20 token1_
    ) internal {
        token0_.safeIncreaseAllowance(vault_, amount0In_);
        token1_.safeIncreaseAllowance(vault_, amount1In_);

        {
            MintRules memory mintRules = mintRestrictedVaults[vault_];
            if (mintRules.supplyCap > 0) {
                require(
                    IArrakisV2(vault_).totalSupply() + mintAmount_ <=
                        mintRules.supplyCap,
                    "above supply cap"
                );
            }
            if (mintRules.hasWhitelist) {
                require(
                    _mintWhitelist[vault_].contains(msg.sender),
                    "not whitelisted"
                );
            }
        }
```

## Tool used

Manual review

## Recommendation

Allow to use [whitelist()](https://github.com/sherlock-audit/2023-06-arrakis/blob/main/v2-periphery/contracts/abstract/ArrakisV2RouterStorage.sol#L109) function without the need to have a positive supply cap. Same in the [blacklist](https://github.com/sherlock-audit/2023-06-arrakis/blob/main/v2-periphery/contracts/abstract/ArrakisV2RouterStorage.sol#L121) function.

```diff
    function whitelist(address vault_, address[] memory toWhitelist_)
        external
        onlyOwner
    {
-       require(mintRestrictedVaults[vault_].supplyCap > 0, "vault not set");
+       require(mintRestrictedVaults[vault_].hasWhitelist, "hasWhitelist not set");
        for (uint256 i; i < toWhitelist_.length; i++) {
            require(_mintWhitelist[vault_].add(toWhitelist_[i]), "add failed");
        }

        emit LogWhitelist(vault_, toWhitelist_);
    }
```

