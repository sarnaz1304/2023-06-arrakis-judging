n33k

high

# Zero liquidity `_ranges` entries can never be removed and potentially lock all the funds

## Summary

`ArrakisV2` has a storage array `_ranges` which is looped through in many locations. There's a logic error inside `rebalance` where zero liquidity `_ranges` entries can never be removed. Growing zero liquidity entries will finally DOS `burn` function and lock all the funds.

## Vulnerability Detail

Only `rebalance` can remove entries from `_ranges`. When burning all the liquidity in the range, the range entry will be removed. But `if (liquidity == 0) continue;` prevents zero liquidity ranges from being removed.

```solidity
                uint128 liquidity = Position.getLiquidityByRange(
                    pool,
                    address(this),
                    rebalanceParams_.burns[i].range.lowerTick,
                    rebalanceParams_.burns[i].range.upperTick
                );

                if (liquidity == 0) continue;

                ......

                if (liquidityToWithdraw == liquidity) {
                    (bool exists, uint256 index) = Position.rangeExists(
                        _ranges,
                        rebalanceParams_.burns[i].range
                    );
                    require(exists, "RRNE");

                    _ranges[index] = _ranges[_ranges.length - 1];
                    _ranges.pop();
                }
```

When there are too many zero liqudity entires left, `burn` function will OOG because it loops through the `_ranges` array. This will lock funds inside the vault forever.

```solidity
    function burn(uint256 burnAmount_, address receiver_)
        external
        nonReentrant
        returns (uint256 amount0, uint256 amount1)
    {
        require(burnAmount_ > 0, "BA");

        uint256 ts = totalSupply();
        require(ts > 0, "TS");

        _burn(msg.sender, burnAmount_);

        Withdraw memory total;
        for (uint256 i; i < _ranges.length; i++) {
```

Here are two scenarios I can think of that will make zero liquitdity ranges:

1. Keeper bot/operator changes strategy and calls rebalance to mints zero liquidity ranges.
3. Keeper bot/operator turns malicious and does a griefing attack.

## Impact

The funds are locked forever inside the vault.

## Code Snippet

https://github.com/sherlock-audit/2023-06-arrakis/blob/main/v2-core/contracts/ArrakisV2.sol#L265-L296

https://github.com/sherlock-audit/2023-06-arrakis/blob/main/v2-core/contracts/ArrakisV2.sol#L175

## Tool used

Manual Review

## Recommendation

```solidity
                uint128 liquidity = Position.getLiquidityByRange(
                    pool,
                    address(this),
                    rebalanceParams_.burns[i].range.lowerTick,
                    rebalanceParams_.burns[i].range.upperTick
                );

                uint128 liquidityToWithdraw;

                if (rebalanceParams_.burns[i].liquidity == type(uint128).max)
                    liquidityToWithdraw = liquidity;
                else liquidityToWithdraw = rebalanceParams_.burns[i].liquidity;

                if (liquidityToWithdraw == liquidity || liquidity == 0) {
                    (bool exists, uint256 index) = Position.rangeExists(
                        _ranges,
                        rebalanceParams_.burns[i].range
                    );
                    require(exists, "RRNE");

                    _ranges[index] = _ranges[_ranges.length - 1];
                    _ranges.pop();
                }

                if (liquidity == 0) continue;

                Withdraw memory withdraw = _withdraw(
                    pool,
                    rebalanceParams_.burns[i].range.lowerTick,
                    rebalanceParams_.burns[i].range.upperTick,
                    liquidityToWithdraw
                );

                aggregator.burn0 += withdraw.burn0;
                aggregator.burn1 += withdraw.burn1;
```