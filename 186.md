vnavascues

medium

# Multiple Chainlink Price Feeds' timestamps of the latest round data are checked against the same value in `ChainLinkOraclePivot._getLatestRoundData()`

## Summary

`ChainLinkOraclePivot` is a Chainlink Price Feed consumer that reads from two data feeds (aka. 2-hop mode) instead of one. As an example, it allows to calculate either the `LINK / USD` or `USD / LINK` pair by combining the latest round data answers of the `LINK / ETH` & `ETH / USD` pairs.

According to Chainlink's docs ([Check the timestamp of the latest answer](https://docs.chain.link/data-feeds#check-the-timestamp-of-the-latest-answer)) each data feed has an individualized Deviation Threshold and Hearbeat configuration. In practice, it means that the amount of time (in seconds) that determines the staleness of the lastest round answer must be individualized per data feed.

## Vulnerability Detail

`ChainLinkOraclePivot` constructor stores in `uint256 public outdated` the delta value (in seconds) that is used to check both price feed (i.e. `priceFeedA` and `priceFeedB`) lastest round data `updatedAt` timestamps (in `ChainLinkOraclePivot._getLatestRoundData()`).

A fitted `outdated` value for `priceFeedA` could cause the revert of reading the `priceFeedB` price (and vice-versa). As an example (using [Ethereum Mainnet Price Feed configurations](https://docs.chain.link/data-feeds/price-feeds/addresses#Ethereum%20Mainnet)):

- Price Feed A: `COMP / USD` with 3600s heartbeat and 1% deviation threshold.
- Price Feed B: `EURT / USD` with 86400s hearbeat and 2% deviation threshold.

Few side effects depending on the `outdated` value:
- Setting `outdated > 3600` seconds would not cause `_getLatestRoundData()` to immediatly revert if Price Feed A failed to report on-chain via its hearbeat. It could also make the Price Feed B read to revert.
- Setting `outdated == 3600` seconds would cause `_getLatestRoundData()` to revert if Price Feed B didn't report on-chain within 3600 seconds since the previous one.
- Setting `outdated < 3600` seconds could cause `_getLatestRoundData()` to revert depending on each Price Feed report timestamp compared to its previous one.

## Impact

Medium

## Code Snippet

From [ChainLinkOraclePivot._getLatestRoundData()](https://github.com/sherlock-audit/2023-06-arrakis/blob/main/v2-manager-templates/contracts/oracles/ChainLinkOraclePivot.sol#L229):

```solidity
    // solhint-disable-next-line function-max-lines
    function _getLatestRoundData()
        internal
        view
        returns (
            uint256 priceA,
            uint256 priceB,
            uint8 priceFeedADecimals,
            uint8 priceFeedBDecimals
        )
    {
        try priceFeedA.latestRoundData() returns (
            uint80,
            int256 price,
            uint256,
            uint256 updatedAt,
            uint80
        ) {
            require(
                block.timestamp - updatedAt <= outdated, // solhint-disable-line not-rely-on-time
                "ChainLinkOracle: priceFeedA outdated."
            );

            priceA = SafeCast.toUint256(price);
        } catch {
            revert("ChainLinkOracle: price feed A call failed.");
        }

        try priceFeedB.latestRoundData() returns (
            uint80,
            int256 price,
            uint256,
            uint256 updatedAt,
            uint80
        ) {
            require(
                block.timestamp - updatedAt <= outdated, // solhint-disable-line not-rely-on-time
                "ChainLinkOracle: priceFeedB outdated."
            );

            priceB = SafeCast.toUint256(price);
        } catch {
            revert("ChainLinkOracle: price feed B call failed.");
        }

        priceFeedADecimals = priceFeedA.decimals();
        priceFeedBDecimals = priceFeedB.decimals();
    }
```

## Tool used

Manual Review

## Recommendation

Have a staleness variable to check against per Price Feed, e.g. `outdatedPriceFeedA` and `outdatedPriceFeedB` instead of a shared `outdated`. 

```solidity
    // solhint-disable-next-line function-max-lines
    function _getLatestRoundData()
        internal
        view
        returns (
            uint256 priceA,
            uint256 priceB,
            uint8 priceFeedADecimals,
            uint8 priceFeedBDecimals
        )
    {
        try priceFeedA.latestRoundData() returns (
            uint80,
            int256 price,
            uint256,
            uint256 updatedAt,
            uint80
        ) {
            require(
                block.timestamp - updatedAt <= outdatedPriceFeedA, // solhint-disable-line not-rely-on-time
                "ChainLinkOracle: priceFeedA outdated."
            );

            priceA = SafeCast.toUint256(price);
        } catch {
            revert("ChainLinkOracle: price feed A call failed.");
        }

        try priceFeedB.latestRoundData() returns (
            uint80,
            int256 price,
            uint256,
            uint256 updatedAt,
            uint80
        ) {
            require(
                block.timestamp - updatedAt <= outdatedPriceFeedB, // solhint-disable-line not-rely-on-time
                "ChainLinkOracle: priceFeedB outdated."
            );

            priceB = SafeCast.toUint256(price);
        } catch {
            revert("ChainLinkOracle: price feed B call failed.");
        }

        priceFeedADecimals = priceFeedA.decimals();
        priceFeedBDecimals = priceFeedB.decimals();
    }
```

