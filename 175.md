immeas

medium

# loss of precision in `ChainLinkOraclePivot`

## Summary
Certain chainlink pairs can have very low precision when calculating price in `ChainLinkOraclePivot`

## Vulnerability Detail
`ChainLinkOraclePivot` calculates prices between pairs where there isn't a chain link price feed between them, thus a combination of feeds must be used.

Lets look at one calculation for `case 3` (this holds true for `case 2` as well for both price calculations):

https://github.com/sherlock-audit/2023-06-arrakis/blob/main/v2-manager-templates/contracts/oracles/ChainLinkOraclePivot.sol#L117-L128
```solidity
File: v2-manager-templates/contracts/oracles/ChainLinkOraclePivot.sol

117:        if (!_ispriceFeedAInversed && _ispriceFeedBInversed) {
118:            return
119:                FullMath.mulDiv(
120:                    FullMath.mulDiv(
121:                        (10 ** (2 * priceFeedBDecimals)) * priceA,
122:                        10 ** token1Decimals,
123:                        priceB
124:                    ),
125:                    1,
126:                    10 ** (priceFeedADecimals + priceFeedBDecimals)
127:                );
128:        }
```

This essentially says:

$$
\frac{10^{pfBDecimals * 2} * P_{A} * 10^{t1_{Decimals}}}{P_{B} * 10^{pfADecimals + pfBDecimals}}
$$

Since any reasonable combination will either be `X/USD`, `Y/USD` or `X/ETH`, `Y/ETH` the pricefeed decimals will be the same, thus the calculation can be simplified to this:

$$\frac{P_{A} * 10^{t1_{Decimals}}}{P_{B}}$$

This will revert if we can find a feed where `priceB ~ priceA * token1Decimals`. I.e. `priceA` must be "`token1Decimals` smaller" than `priceB`.

Looking through chainlink feeds you find: `SPELL/USD` ($0.000490 at time of writing) together with `BTC/USD` ($30,665.93 at time of writing) 

### Proof of Concept

Add the following test to `ChainLinkOraclePivotWrapperTest.t.sol`: based on mainnet block `17564567`
```solidity
    function testSpellBTCPivot() public {
        // mainnet block 17564567

        uint8 token0Decimals = 18;
        uint8 token1Decimals = 8;

        address priceFeedA = 0x8c110B94C5f1d347fAcF5E1E938AB2db60E3c9a8; // spell/usd
        address priceFeedB = 0xF4030086522a5bEEa4988F8cA5B36dbC97BeE88c; // btc/usd
        
        bool isPriceFeedAInversed = false;
        bool isPriceFeedBInversed = true;

        ChainLinkOraclePivot oraclePivot = new ChainLinkOraclePivot(
            token0Decimals,
            token1Decimals,
            priceFeedA,
            priceFeedB,
            address(0),
            100_000,
            isPriceFeedAInversed,
            isPriceFeedBInversed
        );

        uint256 actualPrice0 = oraclePivot.getPrice0(); // spell/btc
        assertEq(1,actualPrice0); // very low precision
    }
```

The same issue is technically possible with a more peculiar combination where `priceFeedBDecimals` is 8 and `priceFeedADecimals` is 18. However, this combination would make no sense, since that would mean a price to fiat USD which isn't possible in a uniswap pool.

## Impact
Certain pairs could have very low precision for determining price.

Granted, there isn't a uniswap pool for this pair but its one example of where discrepancies in price is large enough to cause a problematic precision in price calculations.

## Code Snippet
See above.

## Tool used
Manual Review

## Recommendation
Consider adding a scale factor in the oracle pivot price which could be passed in at construction. That same scale factor should be added in the checks against price in `SimpleManager` later.