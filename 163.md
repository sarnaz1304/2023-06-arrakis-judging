0xbepresent

medium

# `SimpleManager._operators` can block the `ArrakisV2Router.removeLiquidity()` action from the depositors

## Summary

The [SimpleManager._operators](https://github.com/sherlock-audit/2023-06-arrakis/blob/main/v2-manager-templates/contracts/SimpleManager.sol#L56) can use the [SimpleManager.rebalance()](https://github.com/sherlock-audit/2023-06-arrakis/blob/main/v2-manager-templates/contracts/SimpleManager.sol#L128) function to create only [swap operations](https://github.com/sherlock-audit/2023-06-arrakis/blob/main/v2-manager-templates/contracts/SimpleManager.sol#L201-L211) in the vault.

Malicious operator can swap all the available vault amount between the vault tokens causing that the depositor who wants to remove liquidity via the [ArrakisV2Router.removeLiquidity()](https://github.com/sherlock-audit/2023-06-arrakis/blob/main/v2-periphery/contracts/ArrakisV2Router.sol#L201) to be unable to remove beacuase the vault does not have enough token amount and the minimum amount specified in the [removeData_.amountMin](https://github.com/sherlock-audit/2023-06-arrakis/blob/main/v2-periphery/contracts/ArrakisV2Router.sol#L552-L556) will revert the transaction. 

## Vulnerability Detail

The [SimpleManager.rebalance](https://github.com/sherlock-audit/2023-06-arrakis/blob/main/v2-manager-templates/contracts/SimpleManager.sol#L128) function helps to the operator to send operations to the managed vault. The malicious operator can swap all the available from one token to the other token, then the users who expects a [minimum amount](https://github.com/sherlock-audit/2023-06-arrakis/blob/main/v2-periphery/contracts/ArrakisV2Router.sol#L552-L556) from a specific token via the [ArrakisV2Router.removeLiquidity()](https://github.com/sherlock-audit/2023-06-arrakis/blob/main/v2-periphery/contracts/ArrakisV2Router.sol#L536) will be reverted because the minimum amount expected token is not available in the vault. Please see the next scenario:

1. Malicious operator [remove all liquidity from the Uniswap Pools](https://github.com/sherlock-audit/2023-06-arrakis/blob/main/v2-core/contracts/ArrakisV2.sol#L276-L285) via the [SimpleManager.rebalance()](https://github.com/sherlock-audit/2023-06-arrakis/blob/main/v2-manager-templates/contracts/SimpleManager.sol#L128) function.
2. Malicious operator do a [swap token0 to token1](https://github.com/sherlock-audit/2023-06-arrakis/blob/main/v2-core/contracts/ArrakisV2.sol#L334) using all the available amount via the `SimpleManager.rebalance()` function. The vault now has a lot of `token1` and a minimum value of `token0`
3. Some depositors execute the [ArrakisV2Router.removeLiquidity()](https://github.com/sherlock-audit/2023-06-arrakis/blob/main/v2-periphery/contracts/ArrakisV2Router.sol#L201) function with a minimum expected value of `token0`. Since the `vault` have only a few `token0`, the `removeLiquidity()` will be reverted by the [minimum expected requirement](https://github.com/sherlock-audit/2023-06-arrakis/blob/main/v2-periphery/contracts/ArrakisV2Router.sol#L553C24-L553C46).
4. The Malicious operator can do the same blocking attack causing a mistrust in the protocol because the `remove liquidity` from the depositors will be constantly reverted.

Since `Operators` are "semi trusted" I consider the attack is very possible. The documentation says: *Operators are "semi trusted" only to be awake and adhere to the expected vault rebalancing strategy.*

I created a basic test where the `operator` can do a swap using all the token amount available, so the swap using all the available amount is not restricted. Test steps:

1. User msg.sender mints vault tokens.
2. Operator swap all WETH to USDC.
3. WETH balance vault is less than 1 ether.
4. User burns the minted amount and the receiver will receive
   less than 1 WETH which could be a problem if the receiver expects a greater WETH minimum amount.

```solidity
    function testSingleRangeSwapBlockingUserBurn() public {
        // 
        // 1. Mint vault tokens to the msg.sender
        // 2. Swap almost all WETH to USDC
        // 3. WETH balance vault is less than 1 ether
        // 4. Burn the minted amount and the receiver will receive
        //    less than 1 WETH which could be a problem if the receiver expects a greater WETH minimum amount.
        //
        IArrakisV2 vaultV2 = IArrakisV2(vault);
        // make vault to be managed by SimpleManager.
        _rebalanceSetup();
        // get some usdc tokens.
        _getWETHTokens();
        uint256 slot = stdstore.target(vault).sig("init0()").find();
        uint256 init0 = 0;
        vm.store(vault, bytes32(slot), bytes32(init0));
        //  mint some vault tokens.
        (uint256 amount0, uint256 amount1, uint256 mintAmount) = resolver
            .getMintAmounts(vaultV2, 0, AMOUNT_OF_WETH * 2);
        // Approve usdc to the vault
        vm.prank(msg.sender);
        usdc.approve(vault, amount0);
        // Approve weth to the vault
        vm.prank(msg.sender);
        weth.approve(vault, amount1);
        //
        // 1. Mint vault tokens to the msg.sender
        //
        console.log("\nUser balances:");
        console.log("User USDC to mint:    ", amount0);
        console.log("User WETH to mint:    ", amount1 / 10 ** 18);
        vm.prank(msg.sender);
        vaultV2.mint(mintAmount, msg.sender);
        // Assert balances
        assertEq(vaultV2.balanceOf(address(msg.sender)), mintAmount);
        assertEq(usdc.balanceOf(address(vaultV2)), amount0);
        assertEq(weth.balanceOf(address(vaultV2)), amount1);
        console.log("\nMint mintAmount: ", mintAmount);
        console.log("Vault USDC balance:    ", usdc.balanceOf(address(vaultV2)) / 10 ** 8);
        console.log("Vault WETH balance:    ", weth.balanceOf(address(vaultV2)) / 10 ** 18);
        Range memory range = Range({
            lowerTick: lowerTick,
            upperTick: upperTick,
            feeTier: feeTier
        });
        RangeWeight[] memory rangeWeights = new RangeWeight[](1);
        rangeWeights[0] = RangeWeight({weight: 10000, range: range});

        Rebalance memory rebalancePayload = resolver.standardRebalance(
            rangeWeights,
            vaultV2
        );
        (IOracleWrapper oracle_, , uint24 maxSlippage, ) = simpleManager.vaults(
            vault
        );
        //
        // 2. Swap almost all WETH to USDC
        //
        // Amount of weth to swap
        uint256 amountWETHToSwap = weth.balanceOf(address(vaultV2)) - 0.9 ether;
        console.log("\nSwap wethAmount", amountWETHToSwap / 10 ** 18, "to USDC");
        uint256 expectedMinReturn = (FullMath.mulDiv(
            FullMath.mulDiv(
                oracle_.getPrice1(),
                hundred_percent - maxSlippage,
                hundred_percent
            ),
            amountWETHToSwap,
            10 ** ERC20(address(weth)).decimals()
        ) * 10050) / 10000;

        rebalancePayload.swap = SwapPayload({
            router: swapRouter,
            amountIn: amountWETHToSwap,
            expectedMinReturn: expectedMinReturn,
            zeroForOne: false,
            payload: abi.encodeWithSelector(
                ISwapRouter.exactInputSingle.selector,
                ISwapRouter.ExactInputSingleParams({
                    tokenIn: address(weth),
                    tokenOut: address(usdc),
                    fee: feeTier,
                    recipient: vault,
                    deadline: type(uint256).max,
                    amountIn: amountWETHToSwap,
                    amountOutMinimum: 0,
                    sqrtPriceLimitX96: 0
                })
            )
        });
        rebalancePayload.mints[0].liquidity = 1000;
        simpleManager.addOperators(operators);
        simpleManager.rebalance(vault, rebalancePayload);
        //
        // 3. WETH balance vault is less than 1 ether
        //
        uint256 vaultWethBalance = weth.balanceOf(vault);
        uint256 vaultUsdcBalance = usdc.balanceOf(vault);
        assertLt(vaultWethBalance, 1 ether);
        console.log("\nVault usdc/weth balance");
        console.log("Vault USDC balance:    ", vaultUsdcBalance / 10 ** 8);
        console.log("Vault WETH balance:    ", vaultWethBalance / 10 ** 18);
        //
        // 4. Burn the minted amount and the receiver will receive
        // less than 1 WETH which could be a problem if the receiver expects a greater WETH minimum amount.
        //
        console.log("\nBurn mintAmount: ", mintAmount);
        vm.prank(msg.sender);
        vaultV2.burn(mintAmount, address(1337));
        // // Assert weth
        assertLt(weth.balanceOf(address(1337)), 1 ether);
        console.log("Receiver USDC balance: ", usdc.balanceOf(address(1337)) / 10 ** 8);
        console.log("Receiver WETH balance: ", weth.balanceOf(address(1337)) / 10 ** 18);
    }
```

Output:

```bash
User balances:
  User USDC to mint:     0
  User WETH to mint:     199
  
Mint mintAmount:  10626697370244659485
  Vault USDC balance:     0
  Vault WETH balance:     199
  
Swap wethAmount 199 to USDC
  
Vault usdc/weth balance
  Vault USDC balance:     3576
  Vault WETH balance:     0
  
Burn mintAmount:  10626697370244659485
  Receiver USDC balance:  3576
  Receiver WETH balance:  0
```

## Impact

Malicious operators can block the `remove liquidity` action from the depositors, causing a mistrust in the protocol because the protocol has not tokens available.

## Code Snippet

The [SimpleManager.rebalance](https://github.com/sherlock-audit/2023-06-arrakis/blob/main/v2-manager-templates/contracts/SimpleManager.sol#L128) function:

```solidity
File: SimpleManager.sol
128:     function rebalance(
129:         address vault_,
130:         Rebalance calldata rebalanceParams_
131:     ) external {
...
...
149: 
150:         if (mintsLength > 0 || rebalanceParams_.swap.amountIn > 0) {
151:             token0 = address(IArrakisV2(vault_).token0());
152:             token1 = address(IArrakisV2(vault_).token1());
153:             token0Decimals = IDecimals(token0).decimals();
154:             token1Decimals = IDecimals(token1).decimals();
155:         }
...
...
199: 
200:         // check expectedMinReturn on rebalance swap against oracle
201:         if (rebalanceParams_.swap.amountIn > 0) {
202:             _checkMinReturn(
203:                 rebalanceParams_,
204:                 vaultInfo.oracle,
205:                 vaultInfo.maxSlippage,
206:                 token0Decimals,
207:                 token1Decimals
208:             );
209:         }
210: 
211:         IArrakisV2(vault_).rebalance(rebalanceParams_);
212: 
213:         emit RebalanceVault(vault_, msg.sender);
214:     }
```

The [ArrakisV2Router._removeLiquidity()](https://github.com/sherlock-audit/2023-06-arrakis/blob/main/v2-periphery/contracts/ArrakisV2Router.sol#L536) function:

```solidity
File: ArrakisV2Router.sol
536:     function _removeLiquidity(RemoveLiquidityData memory removeData_)
537:         internal
538:         returns (uint256 amount0, uint256 amount1)
539:     {
...
...
552:         require(
553:             amount0 >= removeData_.amount0Min &&
554:                 amount1 >= removeData_.amount1Min,
555:             "received below minimum"
556:         );
...
...
566:     }
```

## Tool used

Manual review

## Recommendation

It is interesting problem because for one side the operators should have the ability to  manage all the vault assets and in the other side the depositans should be able the withdraw all their liquidity. So the easier recommendation is to create a new function called `removeLiquidityAndSwap()`, same as the [_swapAndAddLiquidity()](https://github.com/sherlock-audit/2023-06-arrakis/blob/main/v2-periphery/contracts/ArrakisV2Router.sol#L451C14-L451C34) function. So the depositants have the ability to swap the required token amounts instead of being denied of their liquidity.

